/datum/emote/living/carbon/human
	mob_type_allowed_typecache = list(/mob/living/carbon/human)

/datum/emote/living/carbon/human/cry
	key = "cry"
	key_third_person = "cries"
	message = "плачет."
	emote_type = EMOTE_AUDIBLE
	show_runechat = FALSE

/mob/living/carbon/human/verb/emote_cry()
	set name = "Плакать"
	set category = "Noises"

	emote("cry", intentional = TRUE)

/datum/emote/living/carbon/human/cry/can_run_emote(mob/living/user, status_check = TRUE , intentional)
	. = ..()
	if(. && iscarbon(user))
		var/mob/living/carbon/C = user
		if(C.silent || !C.can_speak())
			message = "makes a noise. Tears stream down their face."


/datum/emote/living/carbon/human/sexmoanlight
	key = "sexmoanlight"
	emote_type = EMOTE_AUDIBLE
	nomsg = TRUE
	only_forced_audio = TRUE

/datum/emote/living/carbon/human/sexmoanlight/can_run_emote(mob/living/user, status_check = TRUE , intentional)
	. = ..()
	if(. && iscarbon(user))
		var/mob/living/carbon/C = user
		if(C.silent || !C.can_speak())
			message = "makes a noise."

/datum/emote/living/carbon/human/sexmoanhvy
	key = "sexmoanhvy"
	emote_type = EMOTE_AUDIBLE
	nomsg = TRUE
	only_forced_audio = TRUE

/datum/emote/living/carbon/human/sexmoanhvy/can_run_emote(mob/living/user, status_check = TRUE , intentional)
	. = ..()
	if(. && iscarbon(user))
		var/mob/living/carbon/C = user
		if(C.silent || !C.can_speak())
			message = "makes a noise."

/datum/emote/living/carbon/human/eyebrow
	key = "eyebrow"
	message = "поднимает бровь."
	emote_type = EMOTE_VISIBLE

/mob/living/carbon/human/verb/emote_eyebrow()
	set name = "Поднять бровь"
	set category = "Emotes"

	emote("eyebrow", intentional = TRUE)

/datum/emote/living/carbon/human/psst
	key = "psst"
	key_third_person = "pssts"
	emote_type = EMOTE_AUDIBLE
	nomsg = TRUE

/mob/living/carbon/human/verb/emote_psst()
	set name = "Пс-с"
	set category = "Noises"

	emote("psst", intentional = TRUE)

/datum/emote/living/carbon/human/grumble
	key = "grumble"
	key_third_person = "grumbles"
	message = "бубнит."
	message_muffled = "makes a grumbling noise."
	emote_type = EMOTE_AUDIBLE
	show_runechat = FALSE

/mob/living/carbon/human/verb/emote_grumble()
	set name = "Бубнеть"
	set category = "Noises"

	emote("grumble", intentional = TRUE)

/datum/emote/living/carbon/human/handshake
	key = "handshake"
	message = "shakes their own hands."
	message_param = "shakes hands with %t."
	restraint_check = TRUE
	emote_type = EMOTE_AUDIBLE


/datum/emote/living/carbon/human/mumble
	key = "mumble"
	key_third_person = "mumbles"
	message = "mumbles."
	emote_type = EMOTE_AUDIBLE

/datum/emote/living/carbon/human/pale
	key = "pale"
	message = "goes pale for a second."

/datum/emote/living/carbon/human/raise
	key = "raise"
	key_third_person = "raises"
	message = "raises a hand."
	restraint_check = TRUE

/datum/emote/living/carbon/human/salute
	key = "salute"
	key_third_person = "salutes"
	message = "salutes."
	message_param = "salutes to %t."
	restraint_check = TRUE

/datum/emote/living/carbon/human/shrug
	key = "shrug"
	key_third_person = "shrugs"
	message = "shrugs."

/datum/emote/living/carbon/human/wag
	key = "wag"

/datum/emote/living/carbon/human/wag/run_emote(mob/user, params, type_override, intentional)
	. = ..()
	if(!.)
		return
	var/mob/living/carbon/human/H = user
	if(!H.dna.species.is_wagging_tail(H))
		H.visible_message(span_biginfo("<span style='color:#[H.voice_color];text-shadow:-1px -1px 0 #000,1px -1px 0 #000,-1px 1px 0 #000,1px 1px 0 #000;'><b>[H]</b></span><span style='color: #c9c1ba;text-shadow:-1px -1px 0 #000,1px -1px 0 #000,-1px 1px 0 #000,1px 1px 0 #000;'> wags [H.p_their()] tail.</span>"), runechat_message = "wags [H.p_their()] tail")
		H.dna.species.start_wagging_tail(H)
	else
		H.visible_message(span_biginfo("<span style='color:#[H.voice_color];text-shadow:-1px -1px 0 #000,1px -1px 0 #000,-1px 1px 0 #000,1px 1px 0 #000;'><b>[H]</b></span></span><span style='color: #c9c1ba;text-shadow:-1px -1px 0 #000,1px -1px 0 #000,-1px 1px 0 #000,1px 1px 0 #000;'> stops wagging [H.p_their()] tail.</span>"), runechat_message = "stops wagging [H.p_their()] tail")
		H.dna.species.stop_wagging_tail(H)

/datum/emote/living/carbon/human/wag/can_run_emote(mob/user, status_check = TRUE , intentional)
	if(!..())
		return FALSE
	var/mob/living/carbon/human/H = user
	return H.dna && H.dna.species && H.dna.species.can_wag_tail(user)

/datum/emote/living/carbon/human/wag/select_message_type(mob/user, intentional)
	. = ..()
	var/mob/living/carbon/human/H = user
	if(H.dna.species.is_wagging_tail(H))
		. = null

/datum/emote/living/carbon/human/wing
	key = "wing"
	key_third_person = "wings"
	message = "flaps their wings."

/mob/living/carbon/human/proc/OpenWings()
	return

/mob/living/carbon/human/proc/CloseWings()
	return

// FEEL EMOTE VERB
/mob/living/carbon/human/verb/emote_feel()
	set name = "Feel (Desire/Dread)"
	set category = "Emotes"

	var/list/options = list("Desire", "Dread")
	var/choice = input(src, "What feeling do you want to express?", "Feel") as null|anything in options
	if(!choice) return

	var/list/degrees = list("mild", "moderate", "strong")
	var/degree = input(src, "Select degree:", "Degree") as null|anything in degrees
	if(!degree) return

	if(choice == "Desire")
		var/desire = input(src, "What is the desire?", "Desire") as null|text
		if(isnull(desire)) return
		var/message = "You [degree == "mild" ? "slightly" : degree == "moderate" ? "moderately" : "strongly"] want to help [src.real_name] fulfil their wish to [desire]"
		if(!length(message) || copytext(message, length(message)) != ".")
			message += "."
		for(var/mob/living/carbon/human/H in viewers(src, null))
			if(HAS_TRAIT(H, TRAIT_EMPATH))
				to_chat(H, "<span style='color: white; font-style: italic; text-shadow: 0 0 6px #fff, 0 0 12px #fff;'>[message]</span>")
		to_chat(src, "You desire [desire].")
		return

	if(choice == "Dread")
		var/dread = input(src, "What are you dreading?", "Dread") as null|text
		if(isnull(dread)) return
		var/message = "You feel [degree]ly negatively preoccupied with the prospect of [dread]."
		if(!length(message) || copytext(message, length(message)) != ".")
			message += "."
		for(var/mob/living/carbon/human/H in viewers(src, null))
			if(HAS_TRAIT(H, TRAIT_EMPATH))
				to_chat(H, "<span style='color: #ff4444; font-weight: bold;'>[message]</span>")
		to_chat(src, "You become preoccupied with [dread].")
		return

/datum/emote/living/carbon/human/wingsfly
	key = "wingsfly"

/datum/emote/living/carbon/human/wingsfly/run_emote(mob/user, params, type_override, intentional)
	. = ..()
	if(!.)
		return
	var/mob/living/carbon/human/H = user
	if(H.has_status_effect(/datum/status_effect/debuff/harpy_flight))
		H.visible_message(
			span_biginfo("<span style='color:#[H.voice_color];text-shadow:-1px -1px 0 #000,1px -1px 0 #000,-1px 1px 0 #000,1px 1px 0 #000;'><b>[H]</b></span></span><span style='color: #c9c1ba;text-shadow:-1px -1px 0 #000,1px -1px 0 #000,-1px 1px 0 #000,1px 1px 0 #000;'> spreads [H.p_their()] wings, preparing to fly!</span>"),
			runechat_message = "spreads [H.p_their()] wings!"
		)
	else
		H.visible_message(
			span_biginfo("<span style='color:#[H.voice_color];text-shadow:-1px -1px 0 #000,1px -1px 0 #000,-1px 1px 0 #000,1px 1px 0 #000;'><b>[H]</b></span></span><span style='color: #c9c1ba;text-shadow:-1px -1px 0 #000,1px -1px 0 #000,-1px 1px 0 #000,1px 1px 0 #000;'> flaps [H.p_their()] wings no more, as [H.p_they()] is back on the ground!</span>"),
			runechat_message = "stops flapping [H.p_their()] wings!"
		)

/mob/living/carbon/human/verb/hand_games()
	set name = "Handgames"
	set desc = "Challenge another to a variety of handgames, which can either be done while standing next to each other or while across a table."
	set category = "Emotes"

	if(stat)
		return

	if((!hand_bodyparts.len))
		to_chat(src, span_warning("You have no hands to play games with!"))
		return

	var/list/nearby = list()
	for(var/mob/living/carbon/human/H in range(src,1))
		if(H.stat)
			continue
		if(H == src)
			continue
		if(!H.hand_bodyparts.len)
			continue
		nearby |= H
	for(var/obj/structure/table/T in range(src, 1))
		for(var/mob/living/carbon/human/H in range(T,1))
			if(H.stat)
				continue
			if(H == src)
				continue
			if(!H.hand_bodyparts.len)
				continue
			nearby |= H

	if(!nearby.len)
		to_chat(src, span_warning("Здесь не с кем играть!"))

	var/partner = tgui_input_list(src, "С кем будем играть?", "FRIEND TO FOE.", nearby)
	if(!partner)
		return
	var/choose_game = tgui_alert(src, "В какую игру будем играть с [partner]?", "A TOURNAMENT FOR TWO.", list("Камень, ножницы, бумага", "Армрестлинг", "Ладушки", "Битва больших пальцев", "Я передмал"))

	if(!choose_game || (choose_game == "Я передмал"))
		return

	if(choose_game == "Камень, ножницы, бумага")
		game_rps(src,partner)

	if(choose_game == "Армрестлинг")
		game_armwrestle(src,partner)

	if(choose_game == "Ладушки")
		game_slaphands(src,partner)

	if(choose_game == "Битва больших пальцев")
		game_thumbwars(src,partner)

// Checks to make sure everything is fine to continue playing.

/mob/living/carbon/human/proc/hand_games_check(var/mob/living/carbon/human/player1, var/mob/living/carbon/human/player2)
	if(!istype(player1) || !istype(player2))
		return 0
	if(player1.stat || player2.stat) //Make sure they're still standing.
		return 0
	if(!(player2 in range(player1,2))) //Make sure they're within two spaces still; should allow for cross-table gaming.
		return 0

	return 1

///// A simple game of Rock Paper Scissors, each player chooses an option and the choices are declared simultaneously.

/mob/living/carbon/human/proc/game_rps(var/mob/living/carbon/human/player1, var/mob/living/carbon/human/player2)
	if(!hand_games_check(player1,player2))
		return
	to_chat(player1, span_notice("Спрашиваем [player2] хочет ли сыграть в 'Камень, ножницы, бумага'..."))
	var/playgame = tgui_alert(player2, "[player1] хочет сыграть в 'Камень, ножницы, бумага'.", "FORTUNE FAVORS THE WITFUL.", list("Играть", "Отказаться"))
	if(!playgame || (playgame == "Отказаться"))
		to_chat(player1, span_warning("[player2] отказывается от игры."))
		return
	else
		player1.visible_message(span_notice("[player1] бросает вызов [player2] в 'Камень, ножницы, бумага'!"))
		to_chat(player2, span_warning("[player1] думает..."))
		var/choice1 = tgui_alert(player1, "Что выбросим?", "DEAL YOUR HAND.", list("Камень", "Бумага", "Ножницы", "Отменить"))
		if(choice1 == "Отменить")
			player1.visible_message(span_notice("[player1] бежит от игры!"))
		if(!hand_games_check(player1,player2))
			return
		to_chat(player1, span_warning("[player2] думает..."))
		var/choice2 = tgui_alert(player2, "Что выбросим?", "DEAL YOUR HAND.", list("Камень", "Бумага", "Ножницы", "Отменить"))
		if(choice2 == "Отменить")
			player2.visible_message(span_notice("[player2] бежит от игры!"))
		if(!hand_games_check(player1,player2))
			return
		if(choice1 == choice2)
			player1.visible_message(span_notice("[player1] и [player2] выбирают [choice1], ничья!"))
		else
			player1.visible_message(span_notice("[player1] выбирает [choice1]!"))
			player2.visible_message(span_notice("[player2] выбирает [choice2]!"))

/////// Armwrestling! Each player gets a modifier based on their size and can choose the strength of their character, then a weighted roll is made.

/mob/living/carbon/human/proc/game_armwrestle(var/mob/living/carbon/human/player1, var/mob/living/carbon/human/player2)
	if(!hand_games_check(player1,player2))
		return
	to_chat(player1, span_notice("Спрашиваем [player2] хочет ли сыграть в 'Армрестлинг'..."))
	var/playgame = tgui_alert(player2, "[player1] хочет сыграть в 'Армрестлинг'.", "TEST YOUR MIGHT.", list("Играть", "Отказаться"))
	if(!playgame || (playgame == "Отказаться"))
		to_chat(player1, span_warning("[player2] отказывается от игры."))
		return
	else
		if(!hand_games_check(player1,player2))
			return
		player1.visible_message(span_notice("[player1] бросает вызов [player2] в 'Армрестлинг'!"))

	var/p1_str = player1.STASTR
	var/p2_str = player2.STASTR
	var/winner = 0
	var/rounds = 14

	for(var/i = 1 to rounds)

		// Has to remain valid, each round.
		if(!hand_games_check(player1, player2))
			return
		// Channel / Struggle moment.
		if(!do_after(player1, 1 SECONDS, target = player2))
			player1.visible_message(span_notice("Игра была прервана!"))
			return
		// Range check to make sure.
		if(get_dist(player1, player2) > 2)
			player1.visible_message(span_warning("Вы находитесь слишком далеко друг от друга!"))
			return

		var/still_near_table = FALSE
		// Both players must remain near a table.
		for(var/obj/structure/table/T in range(player1, 1))
			if(player2 in range(T, 1))
				still_near_table = TRUE
				break

		if(!still_near_table)
			player1.visible_message(span_warning("Соревнования по армрестлингу прерываются, когда участники покидают стол!"))
			return

		//Strength-based stamina damage.
		var/damage_to_p2 = 10 + max(1, p1_str - p2_str)
		var/damage_to_p1 = 10 + max(1, p2_str - p1_str)

		player2.stamina_add(damage_to_p2)
		player1.stamina_add(damage_to_p1)
		// If both players have the same amount of STR, then they'll deal around 11 stamina damage per loop.

		var/p1_exhausted = player1.stamina >= player1.max_stamina	//var for when stamina damage goes above max stam
		var/p2_exhausted = player2.stamina >= player2.max_stamina

		if(p1_exhausted && p2_exhausted)	//Matched exhaustion.
			winner = 3
			break
		else if(p1_exhausted)	//Player 2's triumph.
			winner = 2
			break
		else if(p2_exhausted)	//Player 1's triumph.
			winner = 1
			break

	if(winner == 1)
		player2.Knockdown(20)
		player1.visible_message(span_notice("[player1] побеждает [player2] в игре!"))
	else if(winner == 2)
		player1.Knockdown(20)
		player1.visible_message(span_notice("[player2] побеждает [player1] в игре!"))
	else if(winner == 3)
		player1.Knockdown(20)
		player2.Knockdown(20)
		player1.visible_message(span_notice("Оба игрока падают от истощения!"))
	else
		player1.visible_message(span_notice("Соревнования по армрестлингу закончились ничьей!"))

/////// Slaphands! Each player gets a modifier based on their size and can choose the reaction time of their character, then a weighted roll is made. This one gives the advantage to smaller players.

/mob/living/carbon/human/proc/game_slaphands(var/mob/living/carbon/human/player1, var/mob/living/carbon/human/player2)
	if(!hand_games_check(player1,player2))
		return
	to_chat(player1, span_notice("Спрашиваем [player2] хочет ли сыграть в 'Ладушки'..."))
	var/playgame = tgui_alert(player2, "[player1] хочет сыграть в 'Ладушки'.", "QUICKEST TO THE DRAW.", list("Принять", "Отказаться"))
	if(!playgame || (playgame == "Отказаться"))
		to_chat(player1, span_warning("[player2] отказывается от игры."))
		return
	else
		if(!hand_games_check(player1,player2))
			return
		player1.visible_message(span_notice("[player1] бросает вызов [player2] в 'Ладушки'!"))
		var/speed1 = player1.get_stat(STAT_SPEED)
		var/speed2 = player2.get_stat(STAT_SPEED)
		var/per1 = player1.get_stat(STAT_PERCEPTION)
		var/per2 = player2.get_stat(STAT_PERCEPTION)
		if(!hand_games_check(player1,player2))
			return

		var/score1 = (speed1 + per1)
		var/score2 = (speed2 + per2)

		var/competition = pick(score1;player1, score2;player2)
		if(!do_after(player1, 3 SECONDS, target = player2))
			player2.visible_message(span_notice("Игра отменена!"))
			return 0
		if(!hand_games_check(player1,player2))
			return
		playsound(player1, 'sound/foley/slap.ogg', 30, 1)
		if(competition == player1)
			player1.visible_message(span_notice("[player1] успевает отшлёпать руку [player2] прежде, чем тот успевает среагировать!"))
		else
			player2.visible_message(span_notice("[player2] успевает отшлёпать руку [player1] прежде, чем тот успевает среагировать!"))

///// Thumbwars! This one is pure chance, and - in a pinch - can essentially work like a cointoss.

/mob/living/carbon/human/proc/game_thumbwars(var/mob/living/carbon/human/player1, var/mob/living/carbon/human/player2)
	if(!hand_games_check(player1,player2))
		return
	to_chat(player1, span_notice("Спрашиваем [player2] хочет ли сыграть в 'Битва больших пальцев'..."))
	var/playgame = tgui_alert(player2, "[player1] хочет сыграть в 'Битва больших пальцев'.", "ONE, TWO, THREE, FOUR..", list("Принять", "Отказаться"))
	if(!playgame || (playgame == "Отказаться"))
		to_chat(player1, span_warning("[player2] отказывается от игры."))
		return
	else
		if(!hand_games_check(player1,player2))
			return
		player1.visible_message(span_notice("[player1] бросает вызов [player2] в 'Битва больших пальцев'!"))
		if(!do_after(player1, 5 SECONDS, target = player2))
			player2.visible_message(span_notice("Игра была прервана!!"))
			return 0
		if(!hand_games_check(player1,player2))
			return
		if(prob(50))
			player1.visible_message(span_notice("После короткой битвы, [player1] удаётся положить свой большой палец на палец [player2]!"))
		else
			player2.visible_message(span_notice("После короткой битвы, [player2] удаётся положить свой большой палец на палец [player1]!"))